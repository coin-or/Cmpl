%arg -gn
//
// Robuste Optimierung
//
// Beispiel von Michael Roemer 20.10.2007
// nach Lassmann, Rogge 1990
//

// Koeffizienten und Variablen fuer das Basismodell
const:

	// Produkte (sinnvolle Namen sind mir nicht eingefallen)
	Produkte := set( "A", "B", "C", "D", "E", "F", "G" );

	// Arbeitsschritte
	Arbeitsschritte := set ( "Drehen", "Bohren", "Fraesen", "Montage", "Material" );


	// Erloese pro Produkt
	Erloes[ Produkte ] := (50, 80, 110, 75, 100, 60, 85);

	// Maximal verfuegbare Kapazitaet pro Arbeitsschritt
	Kapazitaet[ Arbeitsschritte ] := (16000, 10000, 15000, 20000, 13000);

	// Aufwaende pro Produkt pro Arbeitsschritt
	Aufwand[ Arbeitsschritte, Produkte ] :=
		((10,  0, 20, 10, 18, 25,  0),
		 ( 5,  8,  0, 12, 14,  0, 19),
		 (20, 15, 18,  0, 26, 17, 18),
		 (10, 16, 17, 18,  0, 20, 21),
		 ( 0,  0, 13, 17, 18,  0, 14));

	// Untere und obere Schranken pro Produkt
	UntereSchranke[ Produkte ] := (200,  50, 250, 350,  50,  50,  70);
	ObereSchranke [ Produkte ] := (280,  80, 300, 400, 100,  85, 150);

var:

	// Variablen fuer Stueckzahl pro Produkt
	// Fuer Stueckzahlen waere wohl prinzipiell Ganzzahligkeit sinnvoll (Datentyp integer),
	// hier aber als stetig definiert
	x[ Produkte ] : real[0..];

// Zusaetze fuer die robuste Optimierung
const:
	// Gamma wird hier ueber die Konsole eingelesen
	// (Stattdessen koennte es natuerlich auch hier direkt festgelegt werden)
	// (Hier wird der Einfachheit halber ein Gamma fuer alle Zeilen verwendet,
	//  statt es zeilenweise verschieden festzulegen; das waere natuerlich genauso gut moeglich)
	Gamma := 3; #import(stdin, "Gamma fuer robuste Optimierung: ");

	// Test, ob Gamma im erlaubten Bereich liegt, sonst Fehlermeldung
	{Gamma < 0:         	error "Gamma darf nicht kleiner als 0 sein"; }
	{Gamma > len(Produkte): error "Gamma darf nicht groesser als die Anzahl der Spalten (" + len(Produkte) + ") sein"; }

	// Maximale Abweichung in den Aufwaenden um 10%
	{i in Arbeitsschritte, j in Produkte:
		Abweichung[i,j] := 0.1 * Aufwand[i,j];
	}

var:
	// Variablen q und p entsprechend der Zeilen- und Spaltenanzahl der Aufwandsmatrix benoetigt
	q[ Arbeitsschritte ] : real[0..];
	p[ Arbeitsschritte, Produkte ] : real[0..];


// Zielfunktion
objective:
	Ziel: Erloes[]T * x[] -> max;
	// (Alternative Schreibweise waere z.B.: Erloes[ Produkte ]T * x[ Produkte ] -> max)
	
// Restriktionen
constraint:
	// Die Restriktionen fuer das Basisproblem koennten einfach geschrieben werden mit:
	//       Aufwand[,] * x[] <= Kapazitaet[];

	// Fuer die robuste Optimierung muss hier stattdessen zeilenweise formuliert werden
	Ress {i in Arbeitsschritte:
		Aufwand[i,]T * x[] + Gamma * q[i] + sum{j in Produkte: p[i,j]} <= Kapazitaet[i];
	}

	// Restriktionen fuer die Abweichung
	Diff {i in Arbeitsschritte, j in Produkte:
		q[i] + p[i,j] >= Abweichung[i,j] * x[j];
	}

	// Schranken
	UntereSchranke[] <= x[] <= ObereSchranke[];

 
