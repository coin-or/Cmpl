parameters:
	price[] := (500, 600, 450 );
	costs[] := (425, 520, 400);

	#machine hours required per unit
	a[,] := ((8, 15, 12), (15, 10, 8));

	#upper bounds of the machines
	b[] := (1000, 1000);

	#profit contribution per unit
	{j:=1(1)dim(price[]):  c[j] := price[j]-costs[j]; }

	#upper bound of a product
	xMax[] := (250, 240, 250 );
	xMin[] := (45, 45, 45 );

	#fixed setup costs
	FC[] := ( 500, 400, 500);

	
variables:
	{j:=1(1)dim(c[]): x[j]: integer[0..xMax[j]]; } 
	y[1..dim(c[])] : binary;


objectives:
	profit: c[]T  * x[] - FC[]T * y[] ->max;

constraints:
	res_$2$: a[,] * x[] <= b[];
	{j:=1(1)dim(c[]): xMin[j] * y[j] <= x[j] <= xMax[j] * y[j]; }
	
	echo "CMPL test finished";







